# Contributor: Aleksander Mietinen <aleksander at mietinen dot net>
# Contributor: Hannes Eichblatt <aur@hanneseichblatt.de>
_base=SecLists
pkgname=${_base,,}
pkgver=2022.3
pkgrel=1
pkgdesc="A collection of multiple types of lists used during security assessments, collected in one place."
arch=(any)
url="https://github.com/danielmiessler/${_base}"
license=(MIT)
source=(${url}/archive/${pkgver}.tar.gz)
sha512sums=('090dac9dc85e9abdaab48e08cabd5ba084e6bc976fb92c329c39482fd4db8a41f984a27812877738cdf4cedf7edcbd48628013336da640636e435c0ebaab273d')

prepare() {
  cd ${_base}-${pkgver}
  find . -iname "rockyou*.tar.gz" -exec sh -c 'tar zxf {} -C $(dirname {}); rm {}' \;
}

package() {
  cd ${_base}-${pkgver}

  # Install to /usr/share/seclists
  find . \( ! -iname "*.md" -a ! -iname ".git*" -a ! -name "LICENSE" \) -type f \
    -exec install -Dm644 {} "${pkgdir}/usr/share/${pkgname}/{}" \;

  # Install all *.md files to /usr/share/doc/seclists
  find . -iname "*.md" -type f \
    -exec install -Dm644 {} "${pkgdir}/usr/share/doc/${pkgname}/{}" \;

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
